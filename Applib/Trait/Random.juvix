module Applib.Trait.Random;

import Stdlib.Prelude open;
import Anoma.Builtin.System as Builtin;
import Anoma open;
import Mtl open;

mkPrng (seed : Nat) : PRNG := Builtin.pseudoRandomNumberGeneratorInit seed;

--- An environment capable of generating random ;ByteArray;s
trait
type Random (M : Type -> Type) :=
  mkRandom@{
    --- Returns a random ;ByteArray; of the given size
    genRandomBytes (numBytes : Nat) : M ByteArray;
  };

open Random using {mkRandom} public;

instance
StateT-Random {M : Type -> Type} {{Monad M}} : Random (StateT PRNG M) :=
  mkRandom@{
    genRandomBytes (n : Nat) : StateT PRNG M ByteArray :=
      StateT.mk (pseudoRandomNumberGeneratorNextBytes n >> pure);
  };

-- TODO use lift
instance
ErrorT-Random
  {Err} {M : Type -> Type} {{Monad M}} {{Random M}} : Random (ErrorT Err M) :=
  mkRandom@{
    genRandomBytes (n : Nat) : ErrorT Err M ByteArray :=
      ErrorT.mk (ok <$> Random.genRandomBytes n);
  };
