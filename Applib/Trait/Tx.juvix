module Applib.Trait.Tx;

import Stdlib.Prelude open;
import Stdlib.Debug.Fail open;
import Anoma open;
import Applib.Data.TxContext open;
import BaseLayer.ResourceMachine open;
import Applib.Trait.Random open;
import Mtl open;

--- An environment used to create Anoma transactions
trait
type Tx (M : Type -> Type) :=
  mkTx@{
    --- Generates a random ;Nonce;
    genRandomNonce : M Nonce;
    getTxContext : M TxContext;
    txThrow {A} : String -> M A;
  };

open Tx using {mkTx} public;

open Tx public;

module Private;
  --- Concrete implementation of the ;Tx; trait
  TxBuilder (A : Type) : Type :=
    ErrorT String (StateT PRNG (ReaderT TxContext Mtl.Identity)) A;

  instance
  TxBuilder-Tx
    : Tx (ErrorT String (StateT PRNG (ReaderT TxContext Mtl.Identity))) :=
    mkTx@{
      txThrow {X} : String -> TxBuilder X := throw;
      getTxContext : TxBuilder TxContext := ask;
      genRandomNonce : TxBuilder Nonce :=
        do {
          bytes <- Random.genRandomBytes 32;
          pure (Nonce.from32SizedByteArray bytes);
        };
    };

  runTxBuilder' {A} (seed : Nat) (si : TxContext) : TxBuilder A -> A :=
    runError
      >> map fromOk
      >> evalState (mkPrng seed)
      >> runReader si
      >> Mtl.run;

  fromOk {A} : Result String A -> A
    | (ok r) := r
    | (error e) := failwith e;
end;

open Private using {TxBuilder} public;

--- Run the transaction builder
runTx (seed : Nat) (si : TxContext) : TxBuilder Transaction -> Transaction :=
  Private.runTxBuilder' seed si;
