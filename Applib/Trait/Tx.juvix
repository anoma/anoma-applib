module Applib.Trait.Tx;

import Stdlib.Prelude open;
import Stdlib.Debug.Fail open;
import Anoma open;
import Applib.Data.TxContext open;
import BaseLayer.ResourceMachine open;
import Applib.Trait.Random open;
import Mtl open;

--- An environment used to create Anoma transactions
trait
type Tx (M : Type -> Type) :=
  mk@{
    --- Generates a random ;Nonce;
    genRandomNonce : M Nonce;
    getTxContext : M TxContext;
    txThrow {A} : String -> M A;
  } with

module Private;
  --- Concrete implementation of the ;Tx; trait
  Builder (A : Type) : Type :=
    ErrorT String (StateT PRNG (ReaderT TxContext Mtl.Identity)) A;

  instance
  TxBuilder-Tx
    : Tx (ErrorT String (StateT PRNG (ReaderT TxContext Mtl.Identity))) :=
    Tx.mk@{
      txThrow {X} : String -> Builder X := throw;
      getTxContext : Builder TxContext := ask;
      genRandomNonce : Builder Nonce :=
        do {
          bytes <- Random.genRandomBytes 32;
          pure (Nonce.from32SizedByteArray bytes);
        };
    };

  runTxBuilder' {A} (seed : Nat) (si : TxContext) : Builder A -> A :=
    runError
      >> map fromOk
      >> evalState (mkPrng seed)
      >> runReader si
      >> Mtl.run;

  fromOk {A} : Result String A -> A
    | (ok r) := r
    | (error e) := failwith e;
end;

open Private using {Builder} public;

--- Run the transaction builder
run (seed : Nat) (si : TxContext) : Builder Transaction -> Transaction :=
  Private.runTxBuilder' seed si;

end;

open Tx hiding {mk} public;
