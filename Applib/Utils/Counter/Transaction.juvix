module Applib.Utils.Counter.Transaction;

import Stdlib.Prelude open;
import Stdlib.Debug.Fail open;
import Stdlib.Debug.Trace open;
import Stdlib.Data.Set as Set open using {Set};
import Anoma.Resource open;
import Anoma.Identity open;
import Anoma.Transaction open;
import Anoma.Utils open;
import Anoma.Builtin.System open;
import Anoma.Proving.Types open;

import Applib.Helpers open;
import Applib.Resource.Error open;
import Applib.Authorization.Message open;
import Applib.Identities open;
import Applib.Utils.Dummy.Resource open;
import Applib.Utils.Counter.Label open;
import Applib.Utils.Counter.Logic open;
import Applib.Utils.Counter.Resource open;
import Applib.Utils.Counter.Count open;
import Applib.Resource.Standards.ValueMap open;
import Applib.Resource.Standards.Ownership open;

import Applib.Helpers open;

-- TODO use general initialize/traits
--- Requires a consumable resource, e.g., `Utils.Dummy.Resource`.
createCounter
  (standardInputs : StandardInputs) (consumable : Resource) : Result StandardError Transaction :=
  let
    identity := StandardInputs.identity standardInputs;
    ephConsumable := consumable@Resource{ephemeral := true};
    uniqueLabel := createUniqueCounterLabel consumable;
    ephCounter :=
      mkCounter@{
        identity;
        ephemeral := true;
        uniqueLabel;
        count := 0
      };
    newCounter :=
      mkCounter@{
        identity;
        ephemeral := false;
        uniqueLabel;
        count := 0
      };
    consumed := Set.fromList [consumable; ephCounter];
    created := Set.fromList [ephConsumable; newCounter];

    nfResMapping := nullifierToResourceMapping identity consumed;
    cmResMapping := commitmentToResourceMapping created;

    nullifiers := NullifiersAndCustomInputs.nullifiers nfResMapping;
    commitments := CommitmentsAndCustomInputs.commitments cmResMapping;

    customInputs :=
      Set.union
        (NullifiersAndCustomInputs.customInputs nfResMapping)
        (CommitmentsAndCustomInputs.customInputs cmResMapping);

  in ok
    mkTransactionHelper@{
      roots := Set.singleton (StandardInputs.currentRoot standardInputs);
      actions :=
        Set.singleton
          mkActionHelper@{
            identity := StandardInputs.identity standardInputs;
            consumed;
            created;
            appData := Set.empty;
            customInputs;
            maybeNullifiers := just nullifiers;
            maybeCommitments := just commitments
          }
    };

-- TODO check kind
incrementCounter (standardInputs : StandardInputs) (currentCounter : Resource) : Transaction :=
  let
    identity := StandardInputs.identity standardInputs;
    label := getCounterLabel currentCounter;
    expectedKind :=
      KindInternal.kind@{
        logicRef := Reference.to counterLogic;
        labelRef := Resource.labelRef currentCounter
      };

    updatedCounter : Resource :=
      let
        valueMap : ValueMap :=
          currentCounter |> Resource.valueRef |> Reference.from |> Value.unValue |> anomaDecode;
        currentCount : Nat :=
          lookupValueMap@{
            key := countValueMapKey;
            valueMap
          };
        updatedValueMap : ValueMap :=
          insertValueMap@{
            key := countValueMapKey;
            value := toValueMapValue (currentCount + 1);
            valueMap
          };
      in currentCounter@Resource{
        ephemeral := false;
        valueRef := Reference.to (updatedValueMap |> anomaEncode |> mkValue)
      };
  in mkTransactionHelper@{
    roots := Set.singleton (StandardInputs.currentRoot standardInputs);
    actions :=
      Set.singleton
        mkActionHelper@{
          identity;
          consumed := Set.singleton currentCounter;
          created := Set.singleton updatedCounter;
          appData := Set.empty;
          customInputs := Set.empty
        }
  };
