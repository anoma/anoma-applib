module Applib.Utils.Dummy.Transaction.Finalize;

import Stdlib.Prelude open;
import Stdlib.Data.Set as Set open using {Set};
import Stdlib.Data.Map as Map;

import Anoma open;

import Applib.Identities open;
import Applib.Utils.Dummy.Resource open;
import Applib.Transaction.Errors open;
import Applib.Helpers open;

finalize (standardInputs : StandardInputs) (dummy : Resource) : Transaction :=
  let
    expectedKind :=
      KindInternal.kind@{
        logicRef := BindingReference.compute alwaysValidLogic;
        labelRef := BindingReference.compute emptyLabel;
      };
    currentKind :=
      kind@{
        resource := dummy;
      };
  in "DEBUG: Evaluating `Applib.Utils.Dummy.Transaction.finalize`"
    >-> fromResult
      case currentKind == expectedKind of {
        | false :=
          throw
            mkInvalidKindError@{
              expected := expectedKind;
              actual := currentKind;
            }
        | true :=
          ok
            mkTransactionHelper@{
              roots :=
                Set.singleton (StandardInputs.currentRoot standardInputs);
              actions :=
                Set.singleton
                  mkActionHelper@{
                    consumed := Set.singleton dummy;
                    created := Set.singleton dummy@Resource{ephemeral := true};
                    appData := Map.empty;
                    customInputs := Map.empty;
                  };
            }
      };
