module Applib.Helpers;

import Stdlib.Prelude open;
import Stdlib.Debug.Fail open using {failwith};

import Applib.Identities open;
import Stdlib.Data.Set as Set open using {Set; empty; insert; union; isMember};
import Stdlib.Data.Map as Map open using {Map};
import Anoma open;
import Anoma.State open;
import Anoma.Builtin.ByteArray open;
import Anoma.Builtin.System open;
import BaseLayer.ResourceMachine as BaseLayer;

import Applib.Resource.Traits.Convertable open;

fromJust {A} : Maybe A -> A
  | nothing := failwith "fromJust"
  | (just x) := x;

findResourceByNullifier
  (nf : Nat) (privateInputs : Logic.Witness) : Maybe Resource :=
  let
    hasNullifier (nf : Nat) (r : Resource) : Bool := base-nullifier r == nf;
    getConsumed (nf : Nat) : Maybe Resource :=
      find (hasNullifier nf) (Logic.Witness.consumed privateInputs);
  in getConsumed nf;

findResourceByCommitment
  (cm : Nat) (privateInputs : Logic.Witness) : Maybe Resource :=
  let
    hasCommitment (cm : Nat) (r : Resource) : Bool := base-commitment r == cm;
    getCreated (cm : Nat) : Maybe Resource :=
      find (hasCommitment cm) (Logic.Witness.created privateInputs);
  in getCreated cm;

type ResourceStatus :=
  | NonExisting
  | CreatedRes Resource
  | ConsumedRes Resource;

fromStatus (status : ResourceStatus) : Resource :=
  case status of
    | CreatedRes r := r
    | ConsumedRes r := r
    | NonExisting := failwith "resource does not exist";

findResource (tag : Nat) (privateInputs : Logic.Witness) : ResourceStatus :=
  case findResourceByNullifier tag privateInputs of
    | just consumed := ConsumedRes consumed
    | nothing :=
      case findResourceByCommitment tag privateInputs of
        | just created := CreatedRes created
        | nothing := NonExisting;

isNullifierPresent
  (nullifier : Nullifier) (nullifierSet : Set Nullifier) : Bool :=
  Set.isMember nullifier nullifierSet;

lookupResource {A} (key : A) (customInputs : CustomInputs) : Maybe Resource :=
  lookupCustomInputs@{
    key;
    customInputs;
  };

toAppDataEntry
  {Key Data : Type}
  (key : Key)
  (data : Data)
  {deletionCriterion : DeletionCriterion := StoreForever}
  : Pair AppDataKey AppDataValue :=

  mkAppDataKey@{
    unAppDataKey := anomaEncode key;
  }
    , mkAppDataValue@{
        data := anomaEncode data;
        deletionCriterion;
      };

toCustomInputsEntry
  {Key Value : Type}
  (key : Key)
  (value : Value)
  : Pair CustomInputsKey CustomInputsValue :=
  mkCustomInputsKey@{
    unCustomInputsKey := anomaEncode key;
  }
    , mkCustomInputsValue@{
        unCustomInputsValue := anomaEncode value;
      };

--- Standard inputs to for transaction functions.
--- NOTE: In the future, this will include more settings such as the information flow control flag.
type StandardInputs :=
  mkStandardInputs@{
    caller : Identity;
    -- currentRoot : CommitmentTree.Root;
    randSeed : RandSeed;
  };

type TagsAndCustomInputs :=
  mkTagsAndCustomInputs@{
    tags : List Tag;
    customInputs : CustomInputs;
  };

tagsToPair (tags : List Tag) : Pair (List Nullifier) (List Commitment) :=
  for (nfs, cms := [], []) (tag in tags) {
    case tag of
      | Consumed nf := nfs ++ [nf], cms
      | Created cm := nfs, cms ++ [cm]
  };

pairtoTags
  (nullifiers : Set Nullifier) (commitments : Set Commitment) : Set Tag :=
  Set.union
    Set.map (nullifier in nullifiers) {
      Consumed nullifier
    }
    Set.map (commitment in commitments) {
      Created commitment
    };

computeTags (consumed created : List Resource) : List Tag :=
  let
    nullifierTags :=
      map (r in consumed) {
        Consumed (nullifier r)
      };
    commitmentTags :=
      map (r in created) {
        Created (commitment r)
      };
  in nullifierTags ++ commitmentTags;

computeTagsAndCustomInputs
  (consumed created : List Resource) : TagsAndCustomInputs :=
  let
    consumed' : List Resource := consumed;
    created' : List Resource := created;

    nullifiers' : List Nullifier := map nullifier consumed';
    commitments' : List Commitment := map commitment created';

    tags' : List Tag := map Consumed nullifiers' ++ map Created commitments';
  in mkTagsAndCustomInputs@{
       tags := tags';
       customInputs :=
         Map.fromList
           (zipWith toCustomInputsEntry tags' (consumed' ++ created'));
     };

module ProvingHelpers;
  createLogicProofRecord
    (tag : Tag)
    (publicInputs : Logic.Instance)
    (privateInputs : Logic.Witness)
    : ProofRecord :=
    let
      proof : Logic.Proof :=
        Logic.prove@{
          provingKey :=
            Logic.mkProvingKey
              (fromStatus (findResource (tagToNat tag) privateInputs));
          publicInputs;
          privateInputs;
        };
      verifyingKey := unit;
      proofRecord := Logic.mkProofRecord proof verifyingKey publicInputs;
    in LogicProofRecord proofRecord;
end;

open ProvingHelpers;

-- Helps with the creation of an action ;Action;.
-- @param identity The identity to derive the nullifier key from.
-- @param consumed The resources being consumed in this action.
-- @param created The resources being created in this action.
-- @param appData The app data of the action.
-- @param customInputs The custom inputs of the action.
-- @param maybeNullifiers Optional nullifiers to avoid recomputation.
-- @param maybeCommitments Optional commitments to avoid recomputation.
-- @return The action object.
mkActionHelper
  (consumed created : List Resource)
  {appData : Nat := 0}
  {custom : CustomInputs := Map.empty}
  : Action :=
  let
    tags :=
      computeTags@{
        consumed;
        created;
      };
    (nullifiers', commitments') := tagsToPair tags;
    nullifiers := map Nullifier.unNullifier nullifiers';
    commitments := map Commitment.unCommitment commitments';

    -- Compute proofs
    logicProofs : List ProofRecord :=
      map (tag in tags) {
        createLogicProofRecord@{
          tag;
          publicInputs :=
            Logic.mkInstance@{
              tag := tagToNat tag;
              commitments;
              nullifiers;
              appData;
            };
          privateInputs :=
            Logic.mkWitness@{
              created;
              consumed;
              customInputs := anomaEncode custom;
            };
        }
      };

    complianceProofs : List ProofRecord := [];
  in mkAction@{
       commitments;
       nullifiers;
       proofs := map proofRecordToProof (logicProofs ++ complianceProofs);
       appData := appData;
     };

logicProofRecordToProof (p : Logic.ProofRecord) : Logic.Proof :=
  Logic.ProofRecord.proof p;

proofRecordToProof : ProofRecord -> Logic.Proof
  | (LogicProofRecord logProof) := logicProofRecordToProof logProof
  | ComplianceProofRecord := failwith "proofRecordToProof"
  | DeltaProofRecord := failwith "proofRecordToProof";

mkTransactionHelper
  (roots : List CommitmentTree.Root) (actions : List Action) : Transaction :=
  mkTransaction@{
    roots := map CommitmentTree.unRoot roots;
    actions;
    delta := BaseLayer.zeroDelta;
    deltaProof := 0;
  };

emptyTx : Transaction :=
  mkTransactionHelper@{
    roots := [];
    actions := [];
  };

--- A helper function preparing the transaction object by creating an action object
--- containing data required by counter resources.
--- @param standardInputs The transaction function standard inputs.
--- @param consumed The consumed resources.
--- @param created The created resources.
--- @return The transaction object.
prepareStandardTransaction
  (standardInputs : StandardInputs)
  (consumed created : List Resource)
  {appData : Nat := 0}
  : Transaction :=
  let
    -- Put maps into the custom inputs that map:
    -- - nullifiers to consumed resources
    -- - commitments to created resources
    tagsAndCustomInputs :=
      computeTagsAndCustomInputs@{
        consumed;
        created;
      };
  in mkTransactionHelper@{
       roots :=  []; --[StandardInputs.currentRoot standardInputs];
       actions :=
         [
           mkActionHelper@{
             consumed;
             created;
             appData;
             custom := TagsAndCustomInputs.customInputs tagsAndCustomInputs;
           };
         ];
     };

-- composeAll (txs : Set Transaction) : Transaction :=
--   for (acc := emptyTx) (tx in txs) {
--     composeTransactions acc tx
--   };

fromResult
  {ErrorType}
  {DataType}
  {{Show ErrorType}}
  (result : Result ErrorType DataType)
  : DataType :=
  case result of
    | ok data := data
    | error err := failwith (Show.show err);
